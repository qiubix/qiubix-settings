#
# A simple theme that displays relevant, contextual information.
# Based on sorin theme 
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   qiubix
#

# Load dependencies.
pmodload 'helper'

function prompt_sorin_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_sorin_pwd="$MATCH"
    unset MATCH
  else
    _prompt_sorin_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_sorin_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_sorin_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_sorin_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_sorin_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{red}❯%F{yellow}❯%F{green}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{green}❮%F{yellow}❮%F{red}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %F{green}✚%f%b'
  zstyle ':prezto:module:git:info:ahead' format ' %F{yellow}⬆%f%b'
  zstyle ':prezto:module:git:info:behind' format ' %F{yellow}⬇%f%b'
  zstyle ':prezto:module:git:info:branch' format '%F{green} %b%f'
  zstyle ':prezto:module:git:info:clean' format '%F{green}✔%f'
  zstyle ':prezto:module:git:info:commit' format '%F{green}➦ %.7c%f'
  zstyle ':prezto:module:git:info:deleted' format ' %F{red}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %F{blue}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format '%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format ' %F{magenta}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %F{cyan}S%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %F{yellow}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %F{white}?%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' %f$(coalesce "%b" "%p" "%c")%s' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'

# extra chars: ◼ ➤ ✭ ✘ ⏎ ⚡
    #'prompt' '$(coalesce "%b" "%p" "%c")' \
#    'prompt' ' %F{blue}git%f$(coalesce "%b" "%p" "%c")%s' \◼

# Define prompts.
PROMPT='╭─ ${SSH_TTY:+"%F{red}%n%f@%F{yellow}%m%f "}%F{blue}${_prompt_sorin_pwd}%f${(e)git_info[ref]}${(e)git_info[prompt]}${git_info[rprompt]}
╰─%(!:%F{red}:)➤${editor_info[keymap]} '
RPROMPT='${editor_info[overwrite]}%(?:: %F{red}✘%f)${VIM:+" %B%F{green}V%f%b"}${INSIDE_EMACS:+" %B%F{green}E%f%b"} %F{blue}[%F{green}%D{%H:%M:%S}%F{blue}]%f'
SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

# backup configs
# ╰─➤%(!. %B%F{yellow}⚡%f%b.)${editor_info[keymap]} '

prompt_sorin_setup "$@"
